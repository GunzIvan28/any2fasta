#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Std;
use File::Basename;
use IO::Uncompress::AnyUncompress;

#......................................................................................

our $VERSION = "0.2.3";
our $EXE = basename($0);
our $FORMAT = "%a %d";
our $STDIN = '/dev/stdin'; # '-' will be replaced with this

#......................................................................................

sub usage {
  my($errcode) = @_;
  $errcode ||= 0;
  my $ofh = $errcode ? \*STDERR : \*STDOUT;
  print $ofh 
    "NAME\n  $EXE $VERSION\n",
    "SYNOPSIS\n  Convert a Genbank/FASTA/GFF/GFA file into a FASTA file\n",
    "USAGE\n  $EXE [options] file.{gb,fa,gff,gfa}[.gz,bz2,zip] > output.fasta\n",
    "OPTIONS\n",
    "  -h       Print this help\n",
    "  -v       Print version and exit\n",
    "  -q       No output while running, only errors\n",
    "  -f STR   FASTA ID format, default='$FORMAT' (Genbank input only)\n",
    "  -n       Replace ambiguous IUPAC letters with 'N'\n",
    "  -l       Lowercase the sequence\n",
    "  -u       Uppercase the sequence\n",
    "FORMAT CODES\n",
    "  %a    Accession  eg. NC_000913\n",
    "  %v    Accession with version  eg. NC_000913.1\n",
    "  %o    Organism  eg. Escherichia coli str. K-12 substr. MG1655\n",
    "  %d    Definition  eg. Escherichia coli str. K-12 substr. MG1655, complete genome\n",
    "END\n";
  exit($errcode);
}

sub version {
  print "$EXE $VERSION\n";
  exit(0);
}

#......................................................................................

my %opt;
getopts('vhqnluf:', \%opt) or exit(-1);
$opt{v} and version();
$opt{h} and usage(0);
@ARGV or usage(1);

#......................................................................................

sub msg {
  print STDERR "@_\n" unless $opt{q};
}

sub err {
  print STDERR "ERROR: @_\n";
  exit(-1);
}

#......................................................................................

msg("This is $EXE $VERSION");

# loop over all positional command line arguments
my $processed=0;

for my $fname (@ARGV) {
  msg("Opening: $fname");
  $fname = $STDIN if $fname eq '-';
  -d $fname and err("'$fname' is a directory not a file");
  -r $fname or err("'$fname' is not readable");

  # read file compressed or otherwise
  my $unzip = IO::Uncompress::AnyUncompress->new($fname);
  my @line = <$unzip>; # slurp all lines
  my $lines = scalar(@line);
  $lines or err("The input is empty");
  msg("Read $lines lines from '$fname'");

  # detect format from first line
  if ($line[0] =~ m/^LOCUS/) { parse_genbank(\@line) }
  elsif ($line[0] =~ m/^>/) { parse_fasta(\@line) }
  elsif ($line[0] =~ m/^##gff/) { parse_gff(\@line) }
  elsif ($line[0] =~ m/^[A-Z]\t\S+/) { parse_gfa(\@line) }
  else { err("Could not determine input sequence format: $line[0]") }

  $processed++;
}

msg("Processed $processed files.");

msg("Done.");
exit(0);

#......................................................................................

sub purify {
  my($dna) = @_;
  $dna =~ s/[^ATGCN\n\r]/N/gi if $opt{n};
  $dna = lc($dna) if $opt{l};
  $dna = uc($dna) if $opt{u};
  return $dna;
}

#......................................................................................

sub parse_fasta {
  my($lines) = @_;
  my $count=0;
  for my $line (@$lines) {
    if ($line =~ m/^>/) {
      $count++;
    }
    else {
      $line = purify($line);
    }
    print $line;
  }
  msg("Found $count sequences in FASTA file.");
}

#......................................................................................

sub parse_gff {
  my($lines) = @_;
  my $at_seq = 0;
  for my $line (@$lines) {
    $at_seq++ if $line =~ m/^>/;
    print purify($line) if $at_seq;;
  }
  if ($at_seq > 0) {
    msg("Found $at_seq sequences in GFF file.");
  }
  else {
    err("No sequences found in GFF file!");
  }
}

#......................................................................................
# https://github.com/GFA-spec/GFA-spec/blob/master/GFA1.md 
# https://github.com/GFA-spec/GFA-spec/blob/master/GFA2.md 

sub parse_gfa {
  my($lines) = @_;
  my %S;
  my %P;
  for my $line (@$lines) {
    my(@x) = split m/\t/, $line;
    # this is NOT the original contigs, rather the unitigs
    # need to parse L (link) and P (path) to reconstruct the contigs
    if ($x[0] eq 'S') {
      print ">", $x[1], "\n", purify($x[2]), "\n";
    }
  }
}

#......................................................................................

sub parse_genbank {
  my($lines) = @_;
  my $acc = '';
  my $accver = '';
  my $org = '';
  my $def = '';
  my $in_seq = 0;
  my $dna = '';
  my $count = 0;

  foreach (@$lines) {
    chomp;
    if (m{^//}) {
      my $id = $opt{f} || $FORMAT;
      $id =~ s/%a/$acc/g;
      $id =~ s/%v/$accver/g;
      $id =~ s/%o/$org/g;
      $id =~ s/%d/$def/;
      $dna = purify($dna);
      print ">", $id, "\n", $dna;
      $count++;
      $in_seq = 0;
      $dna =  $acc = $accver = $org = $def = '';
      next;
    }
    elsif (m/^ORIGIN/) {
      $in_seq = 1;
      next;
    }
    
    if ($in_seq) {
      my $s = substr $_, 10;
      $s =~ s/\s//g;
      $dna .= $s;
      $dna .= "\n";
    }
    else {
      if (m/^VERSION\s+(\S+?\.\d+)\b/) {
        $accver = $1;
      }
      elsif (m/^SOURCE\s+(.*)$/) {
        $org = $1;
      }
      elsif (m/^LOCUS\s+(\S+)/) {
        $acc = $1;
      }
      elsif (m/^DEFINITION\s+(.*)$/) {
        $def = $1;
      }
    }
  }
  msg("Found $count sequences in GENBANK file.");
}

